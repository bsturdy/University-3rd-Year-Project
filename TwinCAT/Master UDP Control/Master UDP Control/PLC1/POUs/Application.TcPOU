<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Application" Id="{e755b4b3-663e-4aba-9984-5644739ccf72}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM Application
VAR
	UdpWin10				: UdpWindowsSocket('192.168.137.1', 10050);
	UdpRt					: UdpRealtimeSocket(10050);
	Udp						: ITF_UdpConnection := UdpRt;
	
	EspFactory				: EspHostFactory;
	List					: List(EspFactory);
	
	MetadataDecoder			: MetadataDecoder(Udp, List);
	UpdaterRegistry			: UpdaterRegistry;
	TestUpdater				: Updater_TestPacket;
	
	Init					: BOOL;
	i						: BYTE;
	j						: UINT;
	State					: BYTE;
	hr						: HRESULT;
	FoundEsp				: BOOL;
	
	TargetEspUID			: ULINT;
	CurrentEspPtr			: POINTER TO EspHost;
	CurrentEsp				: ITF_EspHost;
	CurrentEspMetadata		: EspPacketHeader;
	CurrentUpdater			: ITF_Updater;
	
	ReceivedPacketType		: BYTE;
	ReceivedPacketAddress	: PVOID;
	ReceivedPacketLength	: UINT;
	
	MessageBuffer			: MessageBuffer;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
IF NOT Init THEN

	IF Udp.TryOpenConnection() <> S_OK THEN
		
		RETURN;
	
	END_IF

	UpdaterRegistry.TryRegister(TestUpdater);
	
	Init := TRUE;

END_IF





// Multiple processing calls per cycle are possible
FOR i := 0 TO 9 BY 1 DO
	
	Udp.CyclicUpdate();
	
	CASE State OF
		
		0:
			
			hr := MetadataDecoder.TryFindPacketInBuffer(ReceivedPacketType, ReceivedPacketAddress, ReceivedPacketLength);
			
			IF hr = S_OK THEN
				
				State := 1;
				
			ELSIF hr <> S_PENDING THEN
				
				EXIT;
				
			END_IF
			
			
			
		1:
		
			CurrentUpdater := UpdaterRegistry.ResolveUpdater(ReceivedPacketType, 1);
			
			IF CurrentUpdater = 0 THEN
				
				;// ERROR
				
			END_IF
			
			State := 2;
			
		
		
		2:
		
			TargetEspUID := MetadataDecoder.GetPacketUID();
			
			IF TargetEspUID = 0 THEN
				
				;// ERROR
				
			END_IF
			
			State := 3;
			
			
			
		3:
		
			FOR j := 1 TO List.GetListLength() BY 1 DO
			
				CurrentEspPtr := List.GetNode(j).GetMyPointer();
			
				CurrentEsp := CurrentEspPtr^;
				
				CurrentEspMetadata := CurrentEsp.GetMetadata();
				
				IF TargetEspUID = CurrentEspMetadata.SlaveUid THEN
					
					FoundEsp := TRUE;
					
					EXIT;
					
				END_IF
				
			END_FOR
			
			IF FoundEsp THEN
				
				FoundEsp := FALSE;
				
				State := 5;
				
			ELSE
				
				State := 4;
				
			END_IF
			
			
			
		4:
			
			IF List.CreateNode() THEN
						
				CurrentEspPtr := List.GetNode(List.GetListLength()).GetMyPointer();
				
				CurrentEsp := CurrentEspPtr^;
				
				CurrentEsp.SetUID(TargetEspUID);
				
				CurrentEsp.SetEspType(ReceivedPacketType);
				
				State := 5;
			
			END_IF
		
		
		
		5:
		
			hr := CurrentUpdater.TryApplyPayload(ReceivedPacketAddress, ReceivedPacketLength, CurrentEsp);
			
			IF hr <> S_PENDING AND SUCCEEDED(hr) THEN
				
				CurrentEsp.UpdateMetadata(MetadataDecoder.CreateMetadataUpdate());
				
				MetadataDecoder.ClearBufferAtLocation(ReceivedPacketAddress, ReceivedPacketLength);
				
				ReceivedPacketAddress := 0;
				ReceivedPacketLength := 0;
				ReceivedPacketType := 0;
			
				TargetEspUID := 0;
				CurrentEspPtr := 0;	
				CurrentEsp := 0;
				MEMSET(ADR(CurrentEspMetadata), 0, SIZEOF(CurrentEspMetadata));
				CurrentUpdater := 0;
				
				State := 0;
				
			END_IF
			
	END_CASE

END_FOR
]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>